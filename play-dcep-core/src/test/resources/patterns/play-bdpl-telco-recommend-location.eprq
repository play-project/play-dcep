# 
# Title: Telco Use Case: Recommendation: Based on yesterday and today,
#   there is something worth to be seen near this place!
#
# When Philippe reaches  the Place  Chaban-Delmas, a callEvent is sent to PLAY.
# Then  a CEPAT of PLAY triggers( joining the 140 CallEvents yesterday +  1 
# callEvent  of Philippe today) and pushes a recommendation in the web Portal
# saying that  based on yesterday and today, there is something worth to be
# seen near this Place for Philippe
#
# TODO: -

PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX uctelco: <http://events.event-processing.org/uc/telco/>
PREFIX geo:     <http://www.w3.org/2003/01/geo/wgs84_pos#>
PREFIX esr:     <http://imu.ntua.gr/play/esr/mcm/2#>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>
PREFIX user:    <http://graph.facebook.com/schema/user#>
PREFIX sioc:    <http://rdfs.org/sioc/ns#>
PREFIX :        <http://events.event-processing.org/types/>

CONSTRUCT {
	:e rdf:type :UcTelcoEsrRecom .
	:e :stream <http://streams.event-processing.org/ids/TaxiUCESRRecomDcep#stream> .
	:e :message "Based on yesterday and today, there is something worth to be seen near this place!" .
	:e :members ?e1 .
	:e uctelco:callerPhoneNumber "33638611117" . # Philippe
	:e uctelco:calleePhoneNumber ?callee1 .
	:e uctelco:direction ?direction .
	:e uctelco:ackRequired "true"^^xsd:boolean .
	:e uctelco:answerRequired "true"^^xsd:boolean .
	:e uctelco:action <blank://action1> .
	<blank://action1> rdf:type uctelco:OpenTwitter .
} WHERE {
	EVENT ?id1 {
		?e1 rdf:type :UcTelcoCall .
		?e1 :stream <http://streams.event-processing.org/ids/TaxiUCCall#stream> .
		?e1 uctelco:callerPhoneNumber "33638611117" . # Philippe
		?e1 uctelco:calleePhoneNumber ?callee1 .
		?e1 uctelco:direction ?direction .
		?e1 :location [ geo:lat 48.849249770484356 ; geo:long 2.3039040065044345 ] .
	}
	GRAPH ?id2 {
		?e2 rdf:type :UcTelcoCall .
		?e2 :stream <http://streams.event-processing.org/ids/TaxiUCCall#stream> .
		?e2 :location [ geo:lat ?latitude2 ; geo:long ?longitude2 ] .
		FILTER (abs(48.84755056311516 - ?latitude2) < 0.1 && abs(2.311692827815153 - ?longitude2) < 0.5) # near Esplanade Jacques Chaban Delmas
	}
}
